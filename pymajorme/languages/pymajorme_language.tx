/*
  This is a textX specification of pymajorme_language.tx DSL for generating ORM layer for Java applications.
  Authors: Rade Radišić <radisic DOT rade AT gmail DOT com>
           Daniel Kupčo <kupcodandroid AT gmail DOT com>
*/

ORModel:
    package=Package
    entities+=Entity
    relationships+=Relation
;

Package:
    'package' name=/(\w+\.)*(\w+)/
;

Entity:
    'entity' name=ID '{'
        attributes+=Attribute
    '}'
;

JavaType:
    name=ID
;

Attribute:
    type=[JavaType] name=ID ('as' collection=Collection)? (':' column_parameters*=DBColumnParameter[','])?
;

Collection:
    'set' | 'list' | 'map'
;

Relation:
    source=[Entity] RelationType destination=[Entity]
;

RelationType:
    '->' | '<->' | '--' 
;

DBColumnParameter:
    ColGUID | ColName | ColUnique | ColNullable
;

ColGUID:
    name='GUID'
;

ColName:
    name='name' '=' value=ID
;

ColUnique:
    name='unique' '=' value=TrueFalse
;

ColNullable:
    name='nullable' '=' value=TrueFalse
;

TrueFalse:
    'true' | 'false'
;

// Special rule for comments. Comments start with //
Comment:
  /\/\/.*$/
;
