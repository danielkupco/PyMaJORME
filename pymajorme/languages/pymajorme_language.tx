/*
  This is a textX specification of pymajorme DSL for generating ORM layer for Java applications.
  Authors: Rade Radišić <radisic DOT rade AT gmail DOT com>
           Daniel Kupčo <kupcodandroid AT gmail DOT com>
*/

ORModel:
    package=Package
    entities+=Entity
    relations+=Relation
;

Package:
    'package' name=/(\w+\.)*(\w+)/
;

Entity:
    'entity' name=ID '{'
        attributes+=Attribute
    '}'
;

JavaType:
    name=ID
;

Attribute:
    type=[JavaType] name=ID ('<' collection=Collection '>')? ('[' column_parameters*=DBColumnParameter[','] ']')?
;

Collection:
    'set' | 'list' | 'map'
;

// Relations

Relation:
    source=[Entity] ('<' collection=Collection '>')? ('as' source_name=ID)?
    ('[' (source_column_parameters*=FKColumnParameter[','])? ':' (join_column_parameters*=JoinColumnParameter[','])? ']')?
    relation_type=RelationType
    destination=[Entity] ('<' collection=Collection '>')? ('as' destination_name=ID)?
    ('[' (destination_column_parameters*=FKColumnParameter[','])? ':' (join_column_parameters*=JoinColumnParameter[','])? ']')?
;

RelationType:
    '->' | '<->' | '--' 
;

// Database column attributes

DBColumnParameter:
    ColGUID | ColName | ColUnique | ColNullable
;

FKColumnParameter:
    Cascade | Fetch
;

JoinColumnParameter:
    ReferencedColumn | DBColumnParameter
;

ColGUID:
    name='GUID'
;

ColName:
    name='name' '=' value=ID
;

ColUnique:
    name='unique' '=' value=TrueFalse
;

ColNullable:
    name='nullable' '=' value=TrueFalse
;

TrueFalse:
    'true' | 'false'
;

Cascade:
    name='cascade' '=' '{' values+=CascadeValues[','] '}'
;

CascadeValues:
    'ALL' | ('DETACH' | 'MERGE' | 'PERSIST' | 'REFRESH' | 'REMOVE')
;

Fetch:
    name='fetch' '=' value=FetchValues
;

FetchValues:
    'LAZY' | 'EAGER'
;

ReferencedColumn:
    name='referencedColumnName' '=' value=ID
;

// Special rule for comments. Comments start with //
Comment:
  /\/\/.*$/
;
