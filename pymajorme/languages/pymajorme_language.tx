/*
  This is a textX specification of pymajorme_language.tx DSL for generating ORM layer for Java applications.
  Authors: Rade Radišić <radisic DOT rade AT gmail DOT com>
           Daniel Kupčo <kupcodandroid AT gmail DOT com>
*/

ORModel:
    package=Package
    entities+=Entity
    relations+=Relation
;

Package:
    'package' name=/(\w+\.)*(\w+)/
;

Entity:
    'entity' name=ID '{'
        attributes+=Attribute
    '}'
;

JavaType:
    name=ID
;

Attribute:
    type=[JavaType] name=ID ('as' collection=Collection)? (':' column_parameters*=DBColumnParameter[','])?
;

Collection:
    'set' | 'list' | 'map'
;

Relation:
    source=[Entity] ('['source_name=ID']')? relation_type=RelationType destination=[Entity] ('['destination_name=ID']')? (':' column_parameters*=FKColumnParameter[','])? 
;

RelationType:
    '->' | '<->' | '--' 
;

DBColumnParameter:
    ColGUID | ColName | ColUnique | ColNullable
;

FKColumnParameter:
    Cascade | Fetch
;

ColGUID:
    name='GUID'
;

ColName:
    name='name' '=' value=ID
;

ColUnique:
    name='unique' '=' value=TrueFalse
;

ColNullable:
    name='nullable' '=' value=TrueFalse
;

TrueFalse:
    'true' | 'false'
;

Cascade:
    name='cascade' '=' value=CascadeValues
;

CascadeValues:
    'ALL' | 'DETACH' | 'MERGE' | 'PERSIST' | 'REFRESH' | 'REMOVE'
;

Fetch:
    name='fetch' '=' value=FetchValues
;

FetchValues:
    'LAZY' | 'EAGER'
;

// Special rule for comments. Comments start with //
Comment:
  /\/\/.*$/
;
